apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: "./ktlint.gradle"
apply from: "./dokka.gradle"

rootProject.allprojects {
    repositories {
        maven {
            url "https://cardinalcommerce.bintray.com/android"
            credentials {
                username 'qiao.zhao@cardinalcommerce'
                password '99796fb351b999db8dced5b3f6ba6015efc862e7'
            }
        }
    }
}

def DEVICE_FINGERPRINT_ORG_ID = '"1snn5n9w"'
def DEVICE_FINGERPRINT_MERCHANT_ID = '"airwallex_cybs"'
def API_VERSION_CODE = '"2020-04-22"'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.compileSdkVersion

        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        consumerProguardFiles 'proguard-rules.pro'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    testOptions.unitTests.includeAndroidResources = true
}

configurations {
    javadocDeps
}

ext {
    okhttpVersion = "4.7.2"
    gsonVersion = '2.8.5'
}

android.buildTypes.each { type ->
    type.buildConfigField 'String', 'DEVICE_FINGERPRINT_ORG_ID', DEVICE_FINGERPRINT_ORG_ID
    type.buildConfigField 'String', 'DEVICE_FINGERPRINT_MERCHANT_ID', DEVICE_FINGERPRINT_MERCHANT_ID
    type.buildConfigField 'String', 'API_VERSION_CODE', API_VERSION_CODE
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'

    api "com.squareup.okhttp3:okhttp:$okhttpVersion"
    api "com.google.code.gson:gson:${gsonVersion}"
    // cardinal
    api 'org.jfrog.cardinalcommerce.gradle:cardinalmobilesdk:2.2.3-1'
    // fingerprinting
    api 'com.threatmetrix.TrustDefender:TrustDefender:5.0-96'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutinesVersion"

    javadocDeps 'androidx.annotation:annotation:1.1.0'
    javadocDeps 'androidx.appcompat:appcompat:1.1.0'

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test-annotations-common:$kotlinVersion"

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

ext {
    libraryVersion = "${android.defaultConfig.versionName}"
}

apply from: rootProject.file('publish.gradle')
apply from: rootProject.file('deploy.gradle')