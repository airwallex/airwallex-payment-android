buildscript {
    ext.kotlinVersion = '1.5.30'
    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:1.4.20"
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.5'
    }
}

plugins {
    id "nl.neotech.plugin.rootcoverage" version "1.4.0"
    id "org.sonarqube" version "3.3"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

jacoco {
    // https://www.jacoco.org/jacoco/trunk/doc/changes.html
    toolVersion = "0.8.8"
}

rootCoverage {

    excludes = [
            "**/extension",
            "**/ui",
            "**/log",
            "**/AirwallexStarter*.*",
            "**/card/AirwallexSecurityConnector.class",
            "**/card/ThreeDSecureManager.class",
            "**/*Activity*.*",
            "**/*ActivityLaunch*.*",
            "**/core/Airwallex.class",
            "**/core/AirwallexPaymentManager.class",
            "**/core/AirwallexApiRepository.class",
            // These will be change
            "**/view/PaymentInfoBottomSheetDialog.class",
            "**/view/PaymentMethods*.*"
    ]

    generateHtml true
    generateXml true
    generateCsv false
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// Update the version field when a new version is released
version = '4.0.2'

ext {
    buildToolsVersion = "30.0.2"
    compileSdkVersion = 31
    minSdkVersion = 19

    ktlintVersion = '0.41.0'
    kotlinCoroutinesVersion = '1.5.2'

    wechatPayVersion = "6.6.4"

    junitVersion = '4.13.2'
    robolectricVersion = '4.7'
    testCoreVersion = '1.4.0'

    versionCode = 1
    versionName = version
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
            options.addStringOption('encoding', 'UTF-8')
        }
    }
}

sonarqube {
    properties {
        property 'sonar.host.url', 'https://sonar-dev-cn.awx.onl'
        property 'sonar.projectName', "Airwallex PA SDK Android"
        property 'sonar.projectKey', 'airwallex_airwallex-payment-android'
        property 'sonar.core.codeCoveragePlugin', 'jacoco'
        property 'sonar.coverage.jacoco.xmlReportPaths', "**/build/jacoco/*.xml"
    }
}