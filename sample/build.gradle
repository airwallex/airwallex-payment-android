apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

import java.text.DateFormat
import java.text.SimpleDateFormat

static def getDateTime() {
    DateFormat df = new SimpleDateFormat("MMdd")
    return df.format(new Date())
}

def getApiKey() {
    return readProperty('AIRWALLEX_API_KEY')
}

def getClientId() {
    return readProperty('AIRWALLEX_CLIENT_ID')
}

def getWeChatAppId() {
    return readProperty('AIRWALLEX_WECHAT_APP_ID')
}

def getReturnUrl() {
    return readProperty('AIRWALLEX_RETURN_URL')
}

private def readProperty(name) {
    if (hasProperty(name)) {
        return property(name).trim()
    }
    return ""
}

android {
    namespace 'com.airwallex.paymentacceptance'

    compileSdkVersion rootProject.ext.compileSdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    defaultConfig {
        applicationId "com.airwallex.paymentacceptance"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.compileSdkVersion

        multiDexEnabled true

        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'

        manifestPlaceholders = [
                AIRWALLEX_API_KEY             : getApiKey(),
                AIRWALLEX_CLIENT_ID           : getClientId(),
                AIRWALLEX_WECHAT_APP_ID       : getWeChatAppId(),
                AIRWALLEX_RETURN_URL          : getReturnUrl()
        ]
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "${variant.versionName}-${getDateTime()}-release.apk"
                }
            }
        }
    }

    buildFeatures {
        viewBinding true
    }

    lint {
        abortOnError true
        disable 'VectorPath', 'Autofill', 'LabelFor'
    }
}
apply from: 'releaseTask.gradle'

dependencies {

    api project(":airwallex")
    api project(':card')
    api project(':wechat')
    api project(':redirect')
    api project(':googlepay')

//    implementation 'io.github.airwallex:payment:6.0.1'
//    implementation 'io.github.airwallex:payment-card:6.0.1'
//    implementation 'io.github.airwallex:payment-redirect:6.0.1'
//    implementation 'io.github.airwallex:payment-wechat:6.0.1'
//    implementation 'io.github.airwallex:payment-googlepay:6.0.1'

    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.core:core-splashscreen:1.0.1'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutinesVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.8.2"
    implementation 'androidx.webkit:webkit:1.14.0'

    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.14"
}