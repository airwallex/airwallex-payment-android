apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def computeVersionCode = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', 'HEAD', '--count'
        standardOutput = stdout
    }
    return stdout.toString().trim().toInteger()
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.airwallex.paymentacceptance"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.compileSdkVersion

        versionCode computeVersionCode()
        versionName "1.0.0"


        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    signingConfigs {
        debug {
            storeFile file(AIRWALLEX_STORE_FILE)
            storePassword AIRWALLEX_STORE_PASSWORD
            keyAlias AIRWALLEX_KEY_ALIAS
            keyPassword AIRWALLEX_KEY_PASSWORD
        }
        release {
            storeFile file(AIRWALLEX_STORE_FILE)
            storePassword AIRWALLEX_STORE_PASSWORD
            keyAlias AIRWALLEX_KEY_ALIAS
            keyPassword AIRWALLEX_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            debuggable false
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "${variant.versionName}-${variant.versionCode}-release.apk"
                }
            }
        }
    }
}

dependencies {
    implementation project(':airwallex')
    implementation 'com.google.android.material:material:1.1.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.0.0"

    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'

    // RX
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.16'

    // Retrofit & GSON
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.7.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.7.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.0'

    // Used for Wechat Pay
    implementation 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:5.5.8'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutinesVersion"
}