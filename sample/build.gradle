apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: "./ktlint.gradle"

def getAuthUrl() {
    return readProperty('AIRWALLEX_AUTH_URL')
}

def getBaseUrl() {
    return readProperty('AIRWALLEX_BASE_URL')
}

def getApiKey() {
    return readProperty('AIRWALLEX_API_KEY')
}

def getClientId() {
    return readProperty('AIRWALLEX_CLIENT_ID')
}

def getWeChatAppId() {
    return readProperty('AIRWALLEX_WECHAT_APP_ID')
}

def getWeChatAppSignature() {
    return readProperty('AIRWALLEX_WECHAT_APP_SIGNATURE')
}

private def readProperty(name) {
    if (hasProperty(name)) {
        return property(name).trim()
    }
    return ""
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.airwallex.paymentacceptance"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.compileSdkVersion
        multiDexEnabled true

        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'

        manifestPlaceholders = [
                AIRWALLEX_AUTH_URL            : getAuthUrl(),
                AIRWALLEX_BASE_URL            : getBaseUrl(),
                AIRWALLEX_API_KEY             : getApiKey(),
                AIRWALLEX_CLIENT_ID           : getClientId(),
                AIRWALLEX_WECHAT_APP_ID       : getWeChatAppId(),
                AIRWALLEX_WECHAT_APP_SIGNATURE: getWeChatAppSignature()
        ]
    }

    signingConfigs {
        release {
            storeFile file('airwallex.jks')
            storePassword 'airwallex'
            keyAlias 'airwallex'
            keyPassword 'airwallex'
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            debuggable false
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "${variant.versionName}-release.apk"
                }
            }
        }
    }
}

dependencies {
    implementation project(':airwallex')

//    implementation 'com.airwallex:airwallex-core:2.0.3-support'

    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:preference-v7:28.0.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'

    // WeChat Pay
    implementation 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:5.5.8'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutinesVersion"
//    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0"
}