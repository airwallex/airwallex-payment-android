apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "../ktlint.gradle"

import java.text.DateFormat
import java.text.SimpleDateFormat

static def getDateTime() {
    DateFormat df = new SimpleDateFormat("MMdd")
    return df.format(new Date())
}

def getApiKey() {
    return readProperty('AIRWALLEX_API_KEY')
}

def getClientId() {
    return readProperty('AIRWALLEX_CLIENT_ID')
}

def getWeChatAppId() {
    return readProperty('AIRWALLEX_WECHAT_APP_ID')
}

def getReturnUrl() {
    return readProperty('AIRWALLEX_RETURN_URL')
}

private def readProperty(name) {
    if (hasProperty(name)) {
        return property(name).trim()
    }
    return ""
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.airwallex.paymentacceptance"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.compileSdkVersion

        multiDexEnabled true

        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'

        manifestPlaceholders = [
                AIRWALLEX_API_KEY             : getApiKey(),
                AIRWALLEX_CLIENT_ID           : getClientId(),
                AIRWALLEX_WECHAT_APP_ID       : getWeChatAppId(),
                AIRWALLEX_RETURN_URL          : getReturnUrl()
        ]
    }

    signingConfigs {
        release {
            storeFile file('airwallex.jks')
            storePassword 'airwallex'
            keyAlias 'airwallex'
            keyPassword 'airwallex'
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            debuggable false
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "${variant.versionName}-${getDateTime()}-release.apk"
                }
            }
        }
    }

    buildFeatures {
        viewBinding true
    }

    lintOptions {
        abortOnError true
        disable 'VectorPath', 'Autofill', 'LabelFor'
    }
}

dependencies {

    api project(":airwallex")
    api project(':card')
    api project(':wechat')
    api project(':redirect')

//    implementation 'io.github.airwallex:payment:4.0.0'
//    implementation 'io.github.airwallex:payment-card:4.0.0'
//    implementation 'io.github.airwallex:payment-redirect:4.0.0'
//    implementation 'io.github.airwallex:payment-wechat:4.0.0'

    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutinesVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.0"
}