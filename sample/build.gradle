apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "./ktlint.gradle"

import java.text.DateFormat
import java.text.SimpleDateFormat

static def getDateTime() {
    DateFormat df = new SimpleDateFormat("MMdd")
    return df.format(new Date())
}


def getBaseUrl() {
    return readProperty('AIRWALLEX_BASE_URL')
}

def getApiKey() {
    return readProperty('AIRWALLEX_API_KEY')
}

def getClientId() {
    return readProperty('AIRWALLEX_CLIENT_ID')
}

def getWeChatAppId() {
    return readProperty('AIRWALLEX_WECHAT_APP_ID')
}

private def readProperty(name) {
    if (hasProperty(name)) {
        return property(name).trim()
    }
    return ""
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.airwallex.paymentacceptance"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.compileSdkVersion
        multiDexEnabled true

        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'

        manifestPlaceholders = [
                AIRWALLEX_BASE_URL            : getBaseUrl(),
                AIRWALLEX_API_KEY             : getApiKey(),
                AIRWALLEX_CLIENT_ID           : getClientId(),
                AIRWALLEX_WECHAT_APP_ID       : getWeChatAppId()
        ]
    }

    signingConfigs {
        release {
            storeFile file('airwallex.jks')
            storePassword 'airwallex'
            keyAlias 'airwallex'
            keyPassword 'airwallex'
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            debuggable false
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "${variant.versionName}-${getDateTime()}-release.apk"
                }
            }
        }
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation project(':airwallex')

//    implementation 'io.github.airwallex:airwallex-core:2.0.5'

    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'

    // WeChat Pay
    implementation 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:6.6.4'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutinesVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.1"
}