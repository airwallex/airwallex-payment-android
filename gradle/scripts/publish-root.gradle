ext["signing.password"] = ''
ext["signing.key"] = ''
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''
ext["sonatypeStagingProfileId"] = ''

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
} else {
    ext["signing.password"] = System.getenv('SIGNING_PASSWORD')

    String decoded = decodeBase64Key(System.getenv('SIGNING_KEY_BASE64'))
    if (decoded != null) {
        ext["signing.key"] = decoded
    } else {
        ext["signing.key"] = System.getenv('SIGNING_KEY')
    }

    ext["ossrhUsername"] = System.getenv('OSSRH_USERNAME')
    ext["ossrhPassword"] = System.getenv('OSSRH_PASSWORD')
    ext["sonatypeStagingProfileId"] = System.getenv('SONATYPE_STAGING_PROFILE_ID')
}

static def decodeBase64Key(encodedString) {
    if (encodedString != null) {
        byte[] decoded = encodedString.decodeBase64()
        String decode = new String(decoded)
        return decode
    } else {
        return null
    }
}

nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = sonatypeStagingProfileId
            nexusUrl = uri("https://s01.oss.sonatype.org/service/local/")
            username = ossrhUsername
            password = ossrhPassword
            version = version
        }
    }
}