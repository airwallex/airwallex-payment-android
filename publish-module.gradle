apply plugin: "maven-publish"
apply plugin: "signing"
apply plugin: "org.jetbrains.dokka"

apply from: "${rootDir}/gradle/scripts/publish-base.gradle"

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
    }
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives androidSourcesJar
    archives javadocJar
}

// Unlike the publishing for the fingerprinting modules,
// afterEvaluate is necessary as we need the bundleReleaseAar artifact.
project.afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId airwallexGroupId
                artifactId airwallexArtifactId
                version rootProject.version

                artifact androidSourcesJar
                artifact javadocJar
                artifact bundleReleaseAar

                pom {
                    name = airwallexName
                    description = airwallexDescription
                    url = airwallexUrl
                    licenses {
                        license {
                            name = airwallexLicenseName
                            url = airwallexLicenseUrl
                        }
                    }
                    organization {
                        name = airwallexOrganizationName
                        url = airwallexOrganizationUrl
                    }
                    developers {
                        developer {
                            organization = airwallexOrganizationName
                            organizationUrl = airwallexOrganizationUrl
                        }
                    }
                    scm {
                        connection = airwallexScmConnection
                        developerConnection = airwallexScmConnection
                        url = airwallexScmUrl
                    }
                }

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.api.dependencies.each {
                        if (it.name != 'unspecified') {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group != rootProject.name ? it.group : airwallexGroupId)

                            if (it.group == rootProject.name) {
                                switch (it.name) {
                                    case 'airwallex':
                                        dependencyNode.appendNode('artifactId', 'payment')
                                        break
                                    case 'card':
                                        dependencyNode.appendNode('artifactId', 'payment-card')
                                        break
                                    case 'wechat':
                                        dependencyNode.appendNode('artifactId', 'payment-wechat')
                                        break
                                    case 'redirect':
                                        dependencyNode.appendNode('artifactId', 'payment-redirect')
                                        break
                                    case 'googlepay':
                                        dependencyNode.appendNode('artifactId', 'payment-googlepay')
                                        break
                                    case 'ui-core':
                                        dependencyNode.appendNode('artifactId', "payment-ui-core")
                                        break
                                    case 'components-core':
                                        dependencyNode.appendNode('artifactId', 'payment-components-core')
                                        break
                                    case '3ds':
                                        dependencyNode.appendNode('artifactId', 'payment-3ds')
                                        break
                                    default:
                                        break
                                }
                            } else {
                                dependencyNode.appendNode('artifactId', it.name)
                            }
                            dependencyNode.appendNode('version', it.version != 'unspecified' ? it.version : rootProject.version)
                            dependencyNode.appendNode('scope', 'compile')
                        }
                    }
                    configurations.implementation.dependencies.each {
                        if (it.name != 'unspecified') {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                            dependencyNode.appendNode('scope', 'compile')
                        }
                    }
                }
            }
        }
    }

    signing {
        useInMemoryPgpKeys(
                rootProject.ext["signing.key"],
                rootProject.ext["signing.password"]
        )
        sign publishing.publications
    }
}